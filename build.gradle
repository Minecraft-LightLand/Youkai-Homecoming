import net.darkhax.curseforgegradle.TaskPublishCurseForge

plugins {
    id 'java-library'
    id 'eclipse'
    id 'idea'
    id 'maven-publish'
    id 'net.neoforged.moddev' version '2.0.80'
    id 'net.darkhax.curseforgegradle' version '[1.1.24,)'
    id "at.stnwtr.gradle-secrets-plugin" version "1.0.1"
    id "com.modrinth.minotaur" version "2.+"
}

version = mod_version
group = 'dev.xkmc'

repositories {
    mavenLocal()
}

base {
    archivesName = mod_id
}

java {
    withSourcesJar()
}

java.toolchain.languageVersion = JavaLanguageVersion.of(21)

sourceSets {
    client {}
    server {}
    data {}
    common {}
}

neoForge {
    // We currently only support NeoForge versions later than 21.0.x
    // See https://projects.neoforged.net/neoforged/neoforge for the latest updates
    version = "${neo_version}"

    // Validate AT files and raise errors when they have invalid targets
    // This option is false by default, but turning it on is recommended
    //validateAccessTransformers = true
    accessTransformers.from "./src/main/resources/META-INF/accesstransformer.cfg"

    runs {
        client {
            client()
        }
        data {
            data()

            programArguments.addAll '--mod', project.mod_id, '--all', '--output', file('src/generated/resources/').getAbsolutePath(), '--existing', file('src/main/resources/').getAbsolutePath()

        }
        server {
            server()
        }
    }

    mods {
        "${mod_id}" {
            sourceSet sourceSets.main
        }
    }
}

sourceSets.main.resources { srcDir 'src/generated/resources' }

tasks.withType(ProcessResources).configureEach {
    var replaceProperties = [
            minecraft_version   : minecraft_version, minecraft_version_range: minecraft_version_range,
            neo_version         : neo_version, neo_version_range: neo_version_range,
            loader_version_range: loader_version_range,
            mod_id              : mod_id, mod_name: mod_name, mod_license: mod_license, mod_version: mod_version,
            mod_authors         : mod_authors, mod_description: mod_description,
    ]
    inputs.properties replaceProperties

    filesMatching(['META-INF/neoforge.mods.toml']) {
        expand replaceProperties + [project: project]
    }
}

// Example configuration to allow publishing using the maven-publish plugin
publishing {
    publications {
        register('mavenJava', MavenPublication) {
            from components.java
        }
    }
    repositories {
        maven {
            url "file://${project.projectDir}/repo"
        }
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8' // Use the UTF-8 charset for Java compilation
}

jar {
    manifest {
        attributes([
                "Specification-Title"     : "${mod_id}",
                "Specification-Vendor"    : "xkmc",
                "Specification-Version"   : "1", // We are version 1 of ourselves
                "Implementation-Title"    : project.name,
                "Implementation-Version"  : project.jar.archiveVersion,
                "Implementation-Vendor"   : "xkmc",
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
                'MixinConfigs'            : "${mod_id}.mixins.json"
        ])
    }
}

repositories {
    flatDir {
        dirs 'libs'
    }
    maven { // Registrate
        url "https://maven.ithundxr.dev/snapshots"
    }
    maven { url = "https://maven.createmod.net" }
    maven {
        url = "https://maven.theillusivec4.top/"
        content {
            includeGroup "top.theillusivec4.curios"
        }
    }
    maven {
        // Location of the maven for vazkii's mods
        name 'blamejared'
        url 'https://maven.blamejared.com'
    }
    maven {
        url 'https://www.cursemaven.com'
        content {
            includeGroup "curse.maven"
        }
    }
    maven {
        // Location of the maven that hosts JEI files (and TiC)
        name 'Progwml6 maven'
        url 'https://dvs1.progwml6.com/files/maven'
    }

    maven {
        name = "OctoStudios"
        url = uri("https://maven.octo-studios.com/releases")
    }
    mavenLocal();
}

dependencies {
    compileOnly jarJar(group: 'com.tterrag.registrate', name: 'Registrate', version: "[${registrate_version},)")
    implementation jarJar(group: 'dev.xkmc', name: 'l2serial', version: "[${l2serial_ver},)")
    implementation jarJar(group: 'dev.xkmc', name: 'l2modularblocks', version: "[${l2modularblocks_ver},)")
    implementation jarJar(group: 'dev.xkmc', name: 'l2core', version: "[${l2core_ver},)")
    implementation jarJar(group: 'dev.xkmc', name: 'l2itemselector', version: "[${l2itemselector_ver},)")
    implementation jarJar(group: 'dev.xkmc', name: 'l2damagetracker', version: "[${l2damagetracker_ver},)")
    implementation jarJar(group: 'dev.xkmc', name: 'fast_projectile_api', version: "[${fast_projectile_api_ver},)")
    implementation jarJar(group: 'dev.xkmc', name: 'l2harvester', version: "[${l2harvester_ver},)")

    implementation "mezz.jei:jei-${jei_minecraft_version}:${jei_version}"
    implementation "top.theillusivec4.curios:curios-neoforge:${curios_version}"

    implementation "curse.maven:farmers-delight-398521:5566383"
    implementation "curse.maven:better-advancements-272515:5511371"
    implementation "dev.xkmc:cuisinedelight:1.2.1-slim"
    implementation "dev.xkmc:fruitsdelight:${fruitsdelight_ver}-slim"
    compileOnly "curse.maven:thirst-was-taken-679270:5568715"
    implementation "curse.maven:create-328085:6255497"

    compileOnly("com.simibubi.create:create-1.21.1:6.0.1-13") { transitive = false }
    compileOnly("net.createmod.ponder:Ponder-NeoForge-1.21.1:1.0.39") { transitive = false }
    compileOnly("dev.engine-room.flywheel:flywheel-neoforge-1.21.1:1.0.0-10")
}


tasks.register('publishCurseForge', TaskPublishCurseForge) {
    apiToken = secrets.get("curseforge_token")
    def mainFile = upload(project.curseforge_project, new File("./build/libs/${mod_id}-${mod_version}.jar"))
    mainFile.changelog = new File('./changelog.txt')
    mainFile.addEnvironment("Client", "Server")
    mainFile.addGameVersion("1.21.1")
    mainFile.addModLoader("NeoForge")
    mainFile.addRequirement("farmers-delight")
    mainFile.addOptional(
            "delightful",
            "diet",
            "fruits-delight",
            "serene-seasons",
            "thirst-was-taken",
            "touhou-little-maid")
    mainFile.releaseType = "release"
    disableVersionDetection()
}

modrinth {
    token = secrets.get("modrinth_token")
    projectId = project.modrinth_project
    versionName = "Youkai's Homecoming ${project.mod_version}"
    versionNumber = project.mod_version
    versionType = "release"
    uploadFile = new File("./build/libs/${mod_id}-${mod_version}.jar")
    gameVersions = ["1.21.1"]
    loaders = ["neoforge"]
    changelog = new File('./changelog.txt').text.split("\n\n")[0]
    dependencies {
        required.project "farmers-delight"
        optional.project "fruits-delight"
        optional.project "diet"
        optional.project "delightful"
        optional.project "touhou-little-maid"
        optional.project "thirst-was-taken"
        optional.project "serene-seasons"
    }
}
